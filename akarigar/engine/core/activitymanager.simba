{$include_once ../util/bank.simba}
{$include_once ../util/loader.simba}
{$f-}

const __URL__ = 'http://akarigar.com/srl/activity/%d/next/?items=%s&skills=%s';

type TActivityManager = record
  __activityTimer: TTimeMarker;
  __currentActivity: PActivity;
  __previousStatus: TTaskStatus;
end;

procedure TActivityManager.destroy();
begin
  self.__previousStatus := TTaskStatus.DONE;
  if self.__currentActivity <> nil then
  begin
    self.__currentActivity^.clear();
    dispose(self.__currentActivity);
  end;
  self.__currentActivity := nil;
end;

function TActivityManager.isActivityDone(): boolean;
begin
  exit(
    (__previousStatus <= TTaskStatus.ERR) or
    (self.__currentActivity = nil) or
    (self.__currentActivity^.isDone())
  );
end;

function TActivityManager.next(): PActivity;
var url: string;
begin
  url := self.__getUrl();
  self.destroy();
  self.__currentActivity := Activity(getPage(url));
  self.__activityTimer.reset();
  self.__activityTimer.start();
  exit(self.__currentActivity);
end;

procedure TActivityManager.pause();
begin
  self.__activityTimer.pause();
end;

procedure TActivityManager.printProgress();
var
  i: integer;
  totalXp: extended = 0;
  task: PTask;
begin
  with players[currentPlayer] do
  begin
    writeLn('================================================================');
    writeLn(self.__currentActivity^.name, ' Progress Report');
    writeLn('Player[', currentPlayer, ']: ', nickname);
    writeLn('Played for ', msToTime(self.timeRan(), TIME_FORMAL));
    writeLn('Breaked for ', msToTime(integers[P_TIME_BREAKED], TIME_FORMAL),' (',
            integers[P_BREAK_COUNT], ' breaks)');

    writeLn('Activity looped ', integers[P_LOOP_COUNT], ' times');
    for i := 0 to high(self.__currentActivity^.tasks) do
      totalXp += self.__currentActivity^.tasks[i]^.reportProgress(self.timeRan());

    writeLn(formatProgressLine('Total XP', round(totalXp), self.timeRan()));
    writeLn('================================================================');
  end;
end;

{ Runs the current activity once. }
function TActivityManager.run(): boolean;
var
  i: integer;
  task: PTask;
begin
  if (self.__currentActivity = nil) or (self.__currentActivity^.tasks.size() = 0) then
    exit(false);

  // Start timer if paused
  if self.__activityTimer.paused then
    self.__activityTimer.start();

  for i := 0 to high(self.__currentActivity^.tasks) do
  begin
    task := self.__currentActivity^.tasks[i];
    wait(gaussRangeInt(0, 1200));

    writeLn('Starting task "', task^.name, '"');
    if not playerManager.walkPath(task^.pathToObject, task^.location) then exit(false);
    self.__previousStatus := task^.run();
    writeLn('Task "', task^.name, '" finished with status ', self.__previousStatus);

    if self.__previousStatus <= TTaskStatus.ERR then exit(false);
  end;
  exit(true);
end;

function TActivityManager.timeRan(): LongWord;
begin
  exit(self.__activityTimer.getTime());
end;

function TActivityManager.__getUrl(): string;
var activityId: integer = 0;
begin
  if self.__currentActivity <> nil then activityId := self.__currentActivity^.id;
  result := stringReplace(
    format(
      __URL__,
      [activityId, resourceManager.toString(), playerManager.getSkills()]
    ),
    ' ',
    '%20',
    [rfReplaceAll]
  );
  if playerManager.isMember() then result += '&member=1';
end;

{$f+}
