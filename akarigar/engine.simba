{$IFDEF DEBUG}
  {$IFDEF SMART}
    {$DEFINE SMART_DEBUG}
  {$ENDIF}
{$ENDIF}

{$include_once srl-6/srl.simba}
{$include_once srl-6/lib/misc/srlplayerform.simba}

{$include_once SPS/lib/sps-rs3.simba}

{$include_once engine/util/logger.simba}
{$include_once engine/util/util.simba}

{$include_once engine/core/objectsettings.simba}
{$include_once engine/core/item.simba}
{$include_once engine/core/playermanager.simba}
{$include_once engine/core/resourcemanager.simba}
{$include_once engine/core/task.simba}
{$include_once engine/core/activity.simba}
{$include_once engine/core/activitymanager.simba}

{$include_once engine/util/updater.simba}
{$f-}

type TEngine = record
  declarePlayers: procedure();

  __activityManager: TActivityManager;
  __mouseMoveTimer: TTimeMarker;
  __recentlyClicked: boolean;
end;

var engine: TEngine;

// Setters =====================================================================

procedure TEngine.setAntiBan(antiBan: procedure());
begin
  SRL_Events[EVENT_ANTIBAN] := antiBan;
end;

procedure TEngine.setDeclarePlayers(declarePlayers: procedure());
begin
  self.declarePlayers := declarePlayers;
end;

// End Setters =================================================================

procedure TEngine.notifyClick();
begin
  self.__recentlyClicked := true;
end;

procedure TEngine.__declarePlayers(skills: TIntegerArray);
var
  i, j: integer;
begin
  players.setup(playerForm.players);

  if self.declarePlayers <> nil then
    self.declarePlayers();

  for i := 0 to high(players) do
    with playerForm.players[i] do
    begin
      players[i].world := strToInt(settings[0]);
      players[i].integers[P_PLAY_TIME] := strToInt(settings[1]) * 60000;
      players[i].integers[P_BREAK_LENGTH] := strToInt(settings[2]) * 60000;
      players[i].integers[P_SESSION_TIME] := strToInt(settings[3]) * 60000;

      for j := 0 to high(skills) do
        players[i].booleans[skills[j]] := settings[j + 4] = 'True';
    end;
end;

procedure TEngine.__destroy();
begin
  self.__activityManager.destroy();
  resourceManager.clear();
end;

procedure TEngine.__doAntiBan();
begin
  wait(50 + random(100));
  closeAdWindow();

  if progressScreen.isOpen() then
  begin
    if isMouseOnClient() then mouseOffClient(OFF_CLIENT_RANDOM);
    randomlyWait(750, 1500);
    exit;
  end;

  if (not self.__recentlyClicked) and (self.__mouseMoveTimer.getTime() < 5000) then
  begin
    if random(500) = 0 then
      if self.__activityManager.getRelatedSkill() = SKILL_NONE then
        hoverRandomSkill()
      else
        hoverSkill(self.__activityManager.getRelatedSkill());
    exit;
  end;
  sleepAndMoveMouse(gaussRangeInt(250, 500));

  self.__mouseMoveTimer.start();
  self.__recentlyClicked := false;
  case random(100) of
    0..9: smallRandomMouse(randomRange(50, 200));
    30..50:
      if minimap.isPlayerMoving() then
        mouse(minimap.getBounds().getGaussPoint())
      else
        mouse(mainscreen.getBounds().getGaussPoint());
  end;
end;

procedure TEngine.__fetchSupportedSkills(out skills: TIntegerArray);
var
  data: TStringArray;
  i: integer;
begin
  data := explode(',', getPage('http://srl-akarigar.rhcloud.com/activity/skills/'));
  try
    for i := 0 to high(data) do
      skills.append(strToInt(data[i]));
  except
    writeLn('Fatal error occurred while communicating with server.');
    terminateScript();
  end;
end;

procedure TEngine.__initPlayerForm(skills: TIntegerArray);
var i: integer;
begin
  with playerForm do
  begin
    name := __SCRIPT_NAME;
    scriptSettingsPath := 'akarigar';

    editBoxLabels.combine([
      'World',
      'Take a break every (mins)',
      'Duration of break (mins)',
      'Switch players after (mins)'
    ]);
    editBoxDefaults.combine(['-1', '60', '5', '120']);
    editBoxHints.combine([
      'What world to use? (-1 means use current, 0 means use random)',
      'Approximately how long to play before taking a break? (0 for no breaks)',
      'Approximately how long should every break be?',
      'Approximately how long to play before switching players? (0 to never switch players)'
    ]);

    for i := 0 to high(skills) do
    begin
      checkBoxLabels.append('Train ' + SKILL_NAMES[skills[i]] + '?');
      checkBoxDefaults.append('True');
      checkBoxHints.append('Should I train ' + SKILL_NAMES[skills[i]] + '?');
    end;
  end;
end;

procedure TEngine.__notifyNewPlayer();
begin
  self.__activityManager.destroy();
  playerManager.setup();
  resourceManager.setup(players[currentPlayer].nickname);
end;

procedure TEngine.__runLoop();
begin
  print('TEngine.__runLoop()');
  self.__notifyNewPlayer();
  while players.getActive() > 0 do
  begin
    if players[currentPlayer].login() and randomlyWait(500, 1500) then
    begin
      closeAdWindow();
      closePollWindow();
      exitTreasure();
      mainscreen.setAngle(MS_ANGLE_HIGH);
    end;

    with players[currentPlayer] do
    begin
      // Setup activity if needed & start loop if everything is fine.
      if self.__activityManager.isActivityDone() then
        isActive := self.__activityManager.next() <> nil;

      while playerManager.isActive() and (not self.__activityManager.isActivityDone()) do
      begin
        self.__activityManager.printProgress();
        if not self.__activityManager.run() then break;
      end;
    end;
    logger.debug('isActive: %s', [boolToStr(players[currentPlayer].isActive)]);
    logger.debug('isBreakTime: %s', [boolToStr(playerManager.isBreakTime())]);
    logger.debug('isDone: %s', [boolToStr(self.__activityManager.isActivityDone())]);
    logger.debug('isSessionOver: %s', [boolToStr(playerManager.isSessionOver())]);

    if playerManager.isSessionOver() then
    begin
      players[currentPlayer].logout();
      players.randomNext(players[currentPlayer].isActive);
      self.__notifyNewPlayer();
    end;

    if playerManager.isBreakTime() then
    begin
      self.__activityManager.pause();
      resourceManager.saveResources();
      playerManager.takeBreak();
    end;
  end;
end;

procedure defaultAntiBan();
begin
  engine.__doAntiBan();
end;

function TEngine.run(): boolean;
var skills: TIntegerArray;
begin
  clearDebug();
  updateEngine();
  self.__fetchSupportedSkills(skills);
  self.__initPlayerForm(skills);
  runPlayerForm();
  if not playerForm.isScriptReady then exit(false);

  self.__declarePlayers(skills);
  {$IFNDEF DEBUG}
  disableSRLDebug := true;
  {$ENDIF}
  {$IFDEF SMART}
  smartEnableDrawing := true;
  smartShowConsole := false;
  {$ENDIF}
  if not setupSRL() then exit(false);
  {$IFDEF SMART}
  smartImage.clear();
  {$ENDIF}
  logger.info('Starting up engine.');

  if SRL_Events[EVENT_ANTIBAN] = nil then
    self.setAntiBan(defaultAntiBan);

  self.__recentlyClicked := false;
  self.__mouseMoveTimer.start();
  addOnTerminate('destroyEngine');
  self.__runLoop();
  exit(true);
end;

procedure destroyEngine();
begin
  engine.__destroy();
end;

procedure fastClick(button: integer); override;
begin
  if button = MOUSE_MOVE then exit;
  engine.notifyClick();
  inherited(button);
end;
