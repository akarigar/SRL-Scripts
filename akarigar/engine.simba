{$include_once srl-6/srl.simba}
{$include_once srl-6/lib/misc/srlplayerform.simba}
{$include_once SPS/lib/sps-rs3.simba}

{$include_once lib/core/task.simba}
{$include_once lib/util/random.simba}
{$f-}

const
  // Integers
  P_BREAK_COUNT := 99;
  P_BREAK_LENGTH := P_BREAK_COUNT - 1;
  P_LOOP_COUNT := P_BREAK_LENGTH - 1;
  P_NEXT_BREAK := P_LOOP_COUNT - 1;
  P_PLAY_TIME := P_NEXT_BREAK - 1;
  P_TIME_BREAKED := P_PLAY_TIME - 1;
  P_TIME_RAN := P_TIME_BREAKED - 1;

  // Booleans
  P_TAKE_BREAKS := 99;

type TEngine = record
  antiBan, declarePlayers: procedure();
  showProgress: procedure(timeRan: integer);
  __tasks: TTaskList;
end;

var
  engine: TEngine;
  currentArea: TSPSArea;

// Setters =====================================================================
function TEngine.setAntiBan(antiBan: procedure()): TEngine;
begin
  self.antiBan := antiBan;
  exit(self);
end;

function TEngine.setDeclarePlayers(declarePlayers: procedure()): TEngine;
begin
  self.declarePlayers := declarePlayers;
  exit(self);
end;

function TEngine.setTasks(tasks: TTaskList): TEngine;
begin
  self.__tasks := tasks;
  exit(self);
end;
// End Setters =================================================================

function TEngine.goCloser(symbol, dist: integer = 10): boolean;
var p: TPoint;
begin
  if not minimap.findSymbol(p, symbol, minimap.getBounds()) then
    exit(false);

  if distance(p, minimap.getCenterPoint()) < dist then
    exit(true);

  mouse(p, mouse_Left);
  exit(minimap.waitPlayerMoving());
end

function TEngine.loadArea(mapName: string): TEngine;
begin
  currentArea.setup(mapName, RUNESCAPE_OTHER);
  exit(self);
end;

function TEngine.randomlyWait(min: integer = 250; max: integer = 750): boolean;
begin
  wait(gaussRangeInt(min, max));
  exit(true);
end;

procedure TEngine.takeBreak(time: integer);
var
  timeRemaining: integer;
  timer: TTimeMarker;
begin
  with players[currentPlayer] do
  begin
    timeRemaining := time := getGaussRangePercentage(time);
    writeLn('Taking a break for ', msToTime(timeRemaining, TIME_SHORT));

    if isLoggedIn() then
      if Random() > 0.8 then logout()
      else exitToLobby();

    timer.start();
    while (timeRemaining := time - timer.getTime()) > 45000 do
    begin
      writeLn('Break finishes in ', msToTime(timeRemaining, TIME_SHORT));
      wait(45000);
    end;
    if timeRemaining > 0 then
      wait(timeRemaining);


    inc(integers[P_TIME_BREAKED], timer.getTime());
    inc(integers[P_BREAK_COUNT]);
    integers[P_NEXT_BREAK] := getGaussRangePercentage(integers[P_PLAY_TIME], 0.2);
    writeLn('Break finished. Resuming script');
  end;
end;

function TEngine.useObject(
  color, tolerance: integer;
  colorSettings: TColorSettings;
  bounds: TBox;
  midPoint: TPoint;
  overText: TStringArray
): boolean;
var
  i, x, y: Integer;
  tpa: TPointArray;
  atpa: T2DPointArray;
begin
  if not findColorsSpiralTolerance(x, y, tpa, color, bounds, tolerance, colorSettings) then
    exit(false);

  mainscreen.filterPointsPlayer(tpa);
  atpa := tpa.toATPA(25, 25);
  if midPoint.x = -1 or midPoint.y = -1 then
    atpa.sortBySize()
  else
    atpa.sortFromMidPoint(midPoint);

  for i := 0 to high(atpa) do
  begin
    mouseBox(atpa[i].getBounds());
    if self.randomlyWait() and isMouseOverText(overText)  then
    begin
      fastClick(mouse_left);
      exit(self.randomlyWait() and minimap.waitPlayerMoving());
    end;
  end;
  exit(false);
end;

function TEngine.useObject(
  color, tolerance: integer;
  colorSettings: TColorSettings;
  bounds: TBox;
  midPoint: TPoint;
  overText: TStringArray;
  tries: integer
): boolean; overload;
var i: integer;
begin
  result := false;
  for i := 0 to tries - 1 do
    if self.useObject(color, tolerance, colorSettings, bounds, midPoint, overText) then
      exit(true);
end;

function TEngine.walkPath(path: TPointArray; smybol: integer = -1; distance: integer = 10): boolean;
begin
  if length(path) = 0 then exit(true);
  result := currentArea.walkPath(path) or self.walkTo(path[high(path)]);
  if (not result) and (smybol >= 0) then
    result := self.goCloser(smybol, distance);
end;

{
  Tries to walk to the given point in a straight line.
}
function TEngine.walkTo(dest: TPoint): boolean;
var
  dx, dy, stepX, stepY, i: integer;
  p: TPoint;
  path: TPointArray;
begin
  p := currentArea.getPlayerPos();
  if (p.x = -1) or (p.y = -1) or (dest.x = -1) or (dest.y = -1) then exit(false);
  dx := 20;
  dy := 20;
  if p.x > dest.x then dx *= -1;
  if p.y > dest.y then dy *= -1;
  stepX := floor(abs((dest.x - p.x) / dx));
  stepY := floor(abs((dest.y - p.y) / dy));
  if stepX < stepY then setLength(path, stepY + 1)
  else setLength(path, stepX + 1);

  for i := 0 to high(path) do
  begin
    if i < stepX then p.x += dx
    else p.x := dest.x;
    if i < stepY then p.y += dy
    else p.y := dest.y;
    path[i] := Point(p.x, p.y);
  end;
  exit(currentArea.walkPath(path));
end;

procedure TEngine.__declarePlayers();
var
  i: integer;
begin
  players.setup(playerForm.players);

  if self.declarePlayers <> nil then
    self.declarePlayers();

  for i := 0 to high(players) do
    with players[i], playerForm.players[i] do
    begin
      integers[P_PLAY_TIME] := strToInt(settings[high(playerForm.editBoxLabels) - 1]) * 60000;
      integers[P_BREAK_LENGTH] := strToInt(settings[high(playerForm.editBoxLabels)]) * 60000;
      booleans[P_TAKE_BREAKS] := strToBool(settings[high(settings)]);

      // Set up the time for the next break, if we are taking breaks.
      if booleans[P_TAKE_BREAKS] then
        integers[P_NEXT_BREAK] := getGaussRangePercentage(integers[P_PLAY_TIME], 0.2);
    end;
end;

procedure TEngine.__initForm();
begin
  with playerForm do
  begin
    editBoxLabels.combine(['Take a break around (mins)', 'Duration of break (mins)']);
    editBoxDefaults.combine(['60', '5']);
    editBoxHints.combine(['Around how long to run before taking a break?', 'Around how long should the break be?']);

    checkBoxLabels.combine(['Take Breaks']);
    checkBoxDefaults.combine(['True']);
    checkBoxHints.combine(['Should I take breaks?']);
  end;
end;

function TEngine.__execute(task: TTask): boolean;
var status: TTaskStatus;
begin
  wait(gaussRangeInt(0, 1200));
  writeLn('Starting task "', task.name, '"');
  if not self.walkPath(task.pathToTask) then exit(false);
  status := task.tick();
  writeLn('Task "', task.name, '" finished with status ', status);
  exit(status > TTaskStatus.ERR);
end;

function TEngine.__execute(): boolean; overload;
var currentTask: integer;
var status: TTaskStatus;
begin
  for currentTask := 0 to high(self.__tasks) do
    if not self.__execute(self.__tasks[currentTask]) then exit(false);
  exit(true);
end;


procedure TEngine.__runLoop();
var
  timer: TTimeMarker;
begin
  print('TEngine.__runLoop()');
  SRL_Events[EVENT_ANTIBAN] := self.antiBan;
  while players.getActive() > 0 do
  begin
    with players[currentPlayer] do
    begin
      timer.start();
      if not isLoggedIn() then
        login();
      closeAdWindow();
      closePollWindow();
      exitTreasure();

      while isLoggedIn() and isActive and
          ((not booleans[P_TAKE_BREAKS]) or (timer.getTime() <= integers[P_NEXT_BREAK])) do
      begin
        isActive := self.__execute();
        inc(integers[P_LOOP_COUNT]);

        if self.showProgress <> nil then
          self.showProgress(integers[P_TIME_RAN] + timer.getTime());
      end;
    end;
    inc(players[currentPlayer].integers[P_TIME_RAN], timer.getTime());

    if players[currentPlayer].isActive then
      self.takeBreak(players[currentPlayer].integers[P_BREAK_LENGTH])
    else if players.getActive() > 0 then
      players.randomNext(false);

    if isLoggedIn() then
      players[currentPlayer].logout();
    timer.reset();
  end;
end;

function TEngine.run(): boolean;
begin
  if length(self.__tasks) = 0 then exit(false);

  self.__initForm();
  runPlayerForm();
  if not playerForm.isScriptReady then exit(false);

  self.__declarePlayers();
  if not setupSRL() then exit(false);

{$IFDEF SMART}
  smartEnableDrawing := true;
{$ENDIF}
  self.__runLoop();
  exit(true);
end;
